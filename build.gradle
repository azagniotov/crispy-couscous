buildscript {
    repositories {
        mavenLocal()
        repositories {
            maven {
                url = "https://plugins.gradle.org/m2/"
            }
        }
    }
    dependencies {
        classpath("com.diffplug.spotless:spotless-plugin-gradle:7.0.2")
    }
}


plugins {
    id "java-library"
    id "idea"
    id "eclipse"
    id "jacoco"
    id "maven-publish"
}

defaultTasks 'clean', 'test', 'build'
description = 'Gradle configuration for Crispy Couscous'

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

javadoc {
    if (JavaVersion.current().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

java {
  sourceCompatibility = "${libraryJavaVersion}"
  targetCompatibility = "${libraryJavaVersion}"
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = "https://maven.restlet.talend.com"
    }
}

def getVersion = { ->
    def versionFile = file("latest-version.txt")
    if (!versionFile.exists()) {
        return "MISSING-VERSION"
    }
    def versionFromFile = versionFile.text.trim()
    return versionFromFile
}

jar {
    version = getVersion()
}

apply from: "$rootDir/conf/gradle/dependencies.gradle"
apply from: "$rootDir/conf/gradle/tests.gradle"
apply from: "$rootDir/conf/gradle/spotless.gradle"
apply from: "$rootDir/conf/gradle/publish.gradle"
